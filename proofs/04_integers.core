//Next we prove that the integers exist!
//We construct them as equivalence classes of pairs of natural numbers
//(A, B) is the representative for A - B

NATURALS_X_NATURALS_definition = cross_product(NATURALS, NATURALS)|NATURALS_X_NATURALS|;

prove integer_def_relation_exists: ^R*T0*T1(are_related(T0, T1, R) <-> T0 in NATURALS_X_NATURALS & T1 in NATURALS_X_NATURALS & *X0*Y0*X1*Y1(is_tuple(X0, Y0, T0) & is_tuple(X1, Y1, T1) -> ^S(is_natural_sum(X0, Y1, S) & is_natural_sum(X1, Y0, S)))){
	R_def = relation_existence_lemma[<T0, T1: *X0*Y0*X1*Y1(is_tuple(X0, Y0, T0) & is_tuple(X1, Y1, T1) -> ^S(is_natural_sum(X0, Y1, S) & is_natural_sum(X1, Y0, S)))>](NATURALS_X_NATURALS, NATURALS_X_NATURALS)|R|;
	choose R;
	given |T0, T1|;
	prove lemma0: are_related(T0, T1, R) -> T0 in NATURALS_X_NATURALS & T1 in NATURALS_X_NATURALS & *X0*Y0*X1*Y1(is_tuple(X0, Y0, T0) & is_tuple(X1, Y1, T1) -> ^S(is_natural_sum(X0, Y1, S) & is_natural_sum(X1, Y0, S))){
		implies related_R;
		return R_def(T0, T1)(related_R);
	}
	prove lemma1: T0 in NATURALS_X_NATURALS & T1 in NATURALS_X_NATURALS & *X0*Y0*X1*Y1(is_tuple(X0, Y0, T0) & is_tuple(X1, Y1, T1) -> ^S(is_natural_sum(X0, Y1, S) & is_natural_sum(X1, Y0, S))) -> are_related(T0, T1, R){
		implies R_condition;
		return R_def(T0, T1)(R_condition);
	}
	return iff(lemma0, lemma1);
}

integer_def_relation_def = integer_def_relation_exists|INTEGER_DEFINITION_RELATION|;

prove integer_def_relation_reflexive: *T(T in NATURALS_X_NATURALS -> are_related(T, T, INTEGER_DEFINITION_RELATION)){
	given |T|;
	implies T_in_NATURALS_X_NATURALS;
	prove related_condition: *X0*Y0*X1*Y1(is_tuple(X0, Y0, T) & is_tuple(X1, Y1, T) -> ^S(is_natural_sum(X0, Y1, S) & is_natural_sum(X1, Y0, S))){
		given |X0, Y0, X1, Y1|;
		implies tuple0, tuple1;
		extract tuple_equal_condition(X0, Y0, X1, Y1, T, T)(tuple0, tuple1, equal_sets_reflexive(T)): equal_X, equal_Y;
		extract expand(NATURALS_X_NATURALS_definition)(T)(T_in_NATURALS_X_NATURALS)|X, Y|: X_in_NATURALS, Y_in_NATURALS, tuple_T;
		extract tuple_equal_condition(X0, Y0, X, Y, T, T)(tuple0, tuple_T, equal_sets_reflexive(T)): equal_X0, equal_Y0;
		X0_in_NATURALS = equality_consistency[<Q: Q in NATURALS>](X0, X)(equal_X0)(X_in_NATURALS);
		extract tuple_equal_condition(X1, Y1, X, Y, T, T)(tuple1, tuple_T, equal_sets_reflexive(T)): equal_X1, equal_Y1;
		Y1_in_NATURALS = equality_consistency[<Q: Q in NATURALS>](Y1, Y)(equal_Y1)(Y_in_NATURALS);
		extract natural_sum(X0, Y1)(X0_in_NATURALS, Y1_in_NATURALS)|S|: S_in_NATURALS, sum_S;
		choose S;
		other_sum_S = equality_consistency[<Q: is_natural_sum(Q, Y1, S)>](X0, X1)(equal_X)(sum_S);
		other_sum_S = equality_consistency[<Q: is_natural_sum(X1, Q, S)>](Y0, Y1)(equal_Y)(other_sum_S);
		return sum_S, other_sum_S;
	}
	return integer_def_relation_def(T, T)(T_in_NATURALS_X_NATURALS, T_in_NATURALS_X_NATURALS, related_condition);
}

integer_def_relation_reflexive = #is_reflexive(INTEGER_DEFINITION_RELATION, NATURALS_X_NATURALS)(integer_def_relation_reflexive);

prove integer_def_relation_symmetric: *S*T(are_related(S, T, INTEGER_DEFINITION_RELATION) -> are_related(T, S, INTEGER_DEFINITION_RELATION)){
	given |S, T|;
	implies related_ST;
	extract integer_def_relation_def(S, T)(related_ST): S_in_NATURALS_X_NATURALS, T_in_NATURALS_X_NATURALS, condition_ST;
	prove condition_TS: *X0*Y0*X1*Y1(is_tuple(X0, Y0, T) & is_tuple(X1, Y1, S) -> ^SUM(is_natural_sum(X0, Y1, SUM) & is_natural_sum(X1, Y0, SUM))){
		given |X0, Y0, X1, Y1|;
		implies tuple_T, tuple_S;
		extract condition_ST(X1, Y1, X0, Y0)(tuple_S, tuple_T)|SUM|: sum1, sum0;
		choose SUM;
		return sum0, sum1;
	}
	return integer_def_relation_def(T, S)(T_in_NATURALS_X_NATURALS, S_in_NATURALS_X_NATURALS, condition_TS);
}

integer_def_relation_symmetric = #is_symmetric(INTEGER_DEFINITION_RELATION)(integer_def_relation_symmetric);

prove integer_def_relation_transitive: *R*S*T(are_related(R, S, INTEGER_DEFINITION_RELATION) & are_related(S, T, INTEGER_DEFINITION_RELATION) -> are_related(R, T, INTEGER_DEFINITION_RELATION)){
	given |R, S, T|;
	implies related_RS, related_ST;
	extract integer_def_relation_def(R, S)(related_RS): R_in_NATURALS_X_NATURALS, S_in_NATURALS_X_NATURALS, condition_RS;
	extract integer_def_relation_def(S, T)(related_ST): S_in_NATURALS_X_NATURALS, T_in_NATURALS_X_NATURALS, condition_ST;
	prove condition_RT: *X0*Y0*X1*Y1(is_tuple(X0, Y0, R) & is_tuple(X1, Y1, T) -> ^SUM(is_natural_sum(X0, Y1, SUM) & is_natural_sum(X1, Y0, SUM))){
		given |X0, Y0, X1, Y1|;
		implies tuple_R, tuple_T;
		extract expand(NATURALS_X_NATURALS_definition)(S)(S_in_NATURALS_X_NATURALS)|XS, YS|: XS_in_NATURALS, YS_in_NATURALS, tuple_S;
		extract condition_RS(X0, Y0, XS, YS)(tuple_R, tuple_S)|SUM|: sum_X0_YS, sum_XS_Y0;
		extract condition_ST(XS, YS, X1, Y1)(tuple_S, tuple_T)|OTHER_SUM|: sum_XS_Y1, sum_X1_YS;
	}
}

