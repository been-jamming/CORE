axiom excluded_middle[P]: P | ~P;

prove double_negate[P]: ~~P <-> P{
	prove lemma0: ~~P -> P{
		assume not_not;
		return branch(excluded_middle[<: P>], P_true, P_not){
			return P_true;
		} or {
			return trivial(<: false -> P>)(not_not(P_not));
		};
	}
	prove lemma1: P -> ~~P{
		assume P_true;
		assume P_not;
		return P_not(P_true);
	}
	return iff(lemma0, lemma1);
}

prove not_exists[P(1)]: ~^X(P(X)) <-> *X(~P(X)){
	prove lemma0: ~^X(P(X)) -> *X(~P(X)){
		assume not_exists;
		given |X|;
		assume P_X;
		return not_exists(P_X);
	}
	prove lemma1: *X(~P(X)) -> ~^X(P(X)){
		assume forall_not;
		assume exists;
		P_X = exists|X|;
		return forall_not(X)(P_X);
	}
	return iff(lemma0, lemma1);
}

prove not_forall[P(1)]: ~*X(P(X)) <-> ^X(~P(X)){
	prove lemma0: ~*X(P(X)) -> ^X(~P(X)){
		assume not_forall;
		return branch(excluded_middle[<: ^X(~P(X))>], exists, not_exist){
			return exists;
		} or {
			forall_not_not = not_exists[<W: ~P(W)>](not_exist);
			prove double_negate_var: *W(P(W) <-> ~~P(W)){
				given |W|;
				return double_negate[<: P(W)>];
			}
			contradiction = not_forall(substitute(<Q(1): *W(Q(W))>)[<W: P(W)>, <W: ~~P(W)>](double_negate_var)(forall_not_not));
			return trivial(<: false -> ^X(~P(X))>)(contradiction);
		};
	}
	prove lemma1: ^X(~P(X)) -> ~*X(P(X)){
		assume exists;
		assume forall;
		P_not = exists|X|;
		return P_not(forall(X));
	}
	return iff(lemma0, lemma1);
}

prove or_to_implies[P, Q]: P | Q <-> (~P -> Q){
	prove lemma0: P | Q -> (~P -> Q){
		assume either;
		return branch(either, P_true, Q_true){
			prove result: ~P -> Q{
				assume P_false;
				return P_false(P_true);
			}
			return result;
		} or {
			return trivial(<: Q -> (~P -> Q)>)(Q_true);
		};
	}
	prove lemma1: (~P -> Q) -> P | Q{
		assume implication;
		return branch(excluded_middle[<: P>], P_true, P_false){
			return trivial(<: P -> P | Q>)(P_true);
		} or {
			Q_true = implication(P_false);
			return trivial(<: Q -> P | Q>)(Q_true);
		};
	}
	return iff(lemma0, lemma1);
}

prove not_and_to_or[P, Q]: ~(P & Q) <-> ~P | ~Q{
	prove lemma0: ~(P & Q) -> ~P | ~Q{
		assume not_and;
		return branch(excluded_middle[<: P>], P_true, P_false){
			prove Q_false: ~Q{
				assume Q_true;
				return not_and(P_true, Q_true);
			}
			return or(<: ~P>, Q_false);
		} or {
			return or(P_false, <: ~Q>);
		};
	}
	lemma1 = trivial(<: ~P | ~Q -> ~(P & Q)>);
	return iff(lemma0, lemma1);
}

prove not_or_to_and[P, Q]: ~(P | Q) <-> ~P & ~Q{
	prove lemma0: ~(P | Q) -> ~P & ~Q{
		assume neither;
		prove not_P: ~P{
			assume P_true;
			return neither(P_true);
		}
		prove not_Q: ~Q{
			assume Q_true;
			return neither(Q_true);
		}
		return not_P, not_Q;
	}
	prove lemma1: ~P & ~Q -> ~(P | Q){
		assume not_P, not_Q;
		assume either;
		return branch(either, P_true, Q_true){
			return not_P(P_true);
		} or {
			return not_Q(Q_true);
		};
	}
	return iff(lemma0, lemma1);
}

prove contrapositive_iff[P, Q]: (P <-> Q) <-> (~P <-> ~Q){
	prove lemma0: (P <-> Q) -> (~P <-> ~Q){
		assume P_iff_Q;
		prove lemma0: ~P -> ~Q{
			assume P_false;
			assume Q_true;
			return P_false(P_iff_Q(Q_true));
		}
		prove lemma1: ~Q -> ~P{
			assume Q_false;
			assume P_true;
			return Q_false(P_iff_Q(P_true));
		}
		return iff(lemma0, lemma1);
	}
	prove lemma1: (~P <-> ~Q) -> (P <-> Q){
		assume P_false_iff_Q_false;
		prove lemma0: P -> Q{
			assume P_true;
			P_false_false = trivial(<: P -> ~~P>)(P_true);
			return branch(excluded_middle[<: Q>], Q_true, Q_false){
				return Q_true;
			} or {
				contradiction = P_false_false(P_false_iff_Q_false(Q_false));
				return trivial(<: false -> Q>)(contradiction);
			};
		}
		prove lemma1: Q -> P{
			assume Q_true;
			Q_false_false = trivial(<: Q -> ~~Q>)(Q_true);
			return branch(excluded_middle[<: P>], P_true, P_false){
				return P_true;
			} or {
				contradiction = Q_false_false(P_false_iff_Q_false(P_false));
				return trivial(<: false -> P>)(contradiction);
			};
		}
		return iff(lemma0, lemma1);
	}
	return iff(lemma0, lemma1);
}

